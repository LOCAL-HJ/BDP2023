movies = LOAD '/user/maria_dev/ml-latest/movies.csv'
  USING org.apache.pig.piggybank.storage.CSVExcelStorage(',', 'NO_MULTILINE', 'NOCHANGE', 'SKIP_INPUT_HEADER')
  AS (movieId:int, title:chararray, genres:chararray);
  
ratings = LOAD '/user/maria_dev/ml-latest/ratings.csv'
  USING PigStorage(',')
  AS (userId:int, movieId: int, rating: float, timestamp: chararray);

RatedMovies = GROUP ratings BY movieId;
 
FilteredMovies = FILTER RatedMovies BY COUNT(ratings) >= 30;
 
MovieRatings = FOREACH FilteredMovies GENERATE group AS movieId, COUNT(ratings) AS rating_count, AVG(ratings.rating) AS avg_rating;
MoviesGenres = FOREACH movies GENERATE movieId, FLATTEN(STRSPLITTOBAG(genres, '\\|')) AS genre;
 
GenreRatings = JOIN MovieRatings BY movieId, MoviesGenres BY movieId;
GenreStats = GROUP GenreRatings BY genre;
GenreResults = FOREACH GenreStats GENERATE group AS genre, SUM(GenreRatings.rating_count) AS total_count, AVG(GenreRatings.avg_rating) AS avg_rating;
 
SortedGenreResults = ORDER GenreResults BY total_count DESC;
 
DUMP SortedGenreResults;
 
STORE SortedGenreResults INTO '/user/maria_dev/60213021/best_genre'
USING org.apache.pig.piggybank.storage.CSVExcelStorage(',', 'NO_MULTILINE', 'NOCHANGE', 'WRITE_OUTPUT_HEADER');

